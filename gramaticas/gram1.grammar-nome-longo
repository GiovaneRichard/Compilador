Program -> MainClass ClassDeclarationList
ClassDeclarationList -> ClassDeclaration ClassDeclarationList
ClassDeclarationList -> ''
MainClass -> class ID { public static void main ( String [ ] ID ) { StatementList } }
ClassDeclaration -> class ID { VariableDeclarationList FunctionDeclarationList }
ClassDeclaration -> class ID extends ID { VariableDeclarationList FunctionDeclarationList }
VariableDeclarationList -> VariableDeclaration VariableDeclarationList 
VariableDeclarationList -> ''
VariableDeclaration -> Type ID ;
FunctionDeclarationList -> FunctionDeclaration FunctionDeclarationList
FunctionDeclarationList -> ''
FunctionDeclaration -> public Type ID ( ParameterList ) { VariableDeclarationList StatementList return Expression ; }
ParameterList -> Type ID ParameterListRest
ParameterList -> ''
ParameterListRest -> , Type ID ParameterListRest
ParameterListRest -> ''
Type -> int [ ]
Type -> boolean
Type -> int
Type -> ID
StatementList -> Statement StatementList
StatementList -> ''
Statement -> { StatementList }
Statement -> if ( Expression ) Statement else Statement
Statement -> while ( Expression ) Statement
Statement -> System.out.println ( Expression ) ;
Statement -> ID = Expression ;
Statement -> ID [ Expression ] = Expression ;
Expression -> Expression && Expression
Expression -> Expression || Expression
Expression -> Expression < Expression
Expression -> Expression + Expression
Expression -> Expression - Expression
Expression -> Expression * Expression
Expression -> Expression [ Expression ]
Expression -> Expression . length
Expression -> Expression . ID ( ExpressionList )
Expression -> NUM_DECIM
Expression -> BOOLEAN_VALUE
Expression -> ID
Expression -> this
Expression -> new int [ Expression ]
Expression -> new ID ( )
Expression -> ! Expression
Expression -> ( Expression )
ExpressionList -> ''
ExpressionList -> Expression ExpressionListRest
ExpressionListRest -> , Expression ExpressionListRest
ExpressionListRest -> ''
